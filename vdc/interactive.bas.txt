	bank 15
	vv = peek(dec("d600")) and 7
	graphic 0
	print "{down}vdc version is" vv
	print "switch monitor to vdc!{down}"
	slow:rem make sure use can see vdc version output on vic
	graphic 5
	wx = dec("cdcc"):rem write a to vdc reg x
	rx = dec("cdda"):rem read vdc reg x
	ch = 8:rem character height (r23)
	cw = 8:rem character width (r22:3210)
	dd = 87:rem display disable (r35)
	de = 6:rem display enable (r34)
	fb = 0:rem flag for bitmap (r25:7)
	fr = 64:rem flag for reverse (r24:6)
	fs = 0:rem flag for semi graphics (r25:5)
	ll = 78:rem line length (r1)
	li = 1:rem line increment (r27)
	ul = 7:rem underline (r29)
	xs = 0:rem xscroll (v0 vdc)
	ys = 0:rem yscroll
	if vv then ll = 79:xs = 7:rem adjust for v1/v2 vdcs
	color 6, 2
	print "{home}{home}{blk}{swlc}{clr}{CBM-+}{CBM-+}{CBM-+}{CBM-+}" chr$(27) "r
	print "{CBM-+} interactive vdc test program 6
	print "{CBM-+}
	print "{CBM-+} vdc version:" vv
	print "{CBM-+}
	print "{CBM-+}   x scroll:
	print "{CBM-+}   y scroll:
	print "{CBM-+} w - r22lo, char width:
	print "{CBM-+} h - r23, char height-1:
	print "{CBM-+} l - r1, line length:
	print "{CBM-+} i - r27, line increment:
	print "{CBM-+} u - r29, underline:
	print "{CBM-+} e - r34, enable disp:
	print "{CBM-+} d - r35, disable disp:
	print "{CBM-+}   flags:
	print "{CBM-+} b - bitmap:
	print "{CBM-+} r - reverse:
	print "{CBM-+} s - semigraphics:
	rem show all values
	gosub 250:rem yscroll
	gosub 270:rem reverse flag
	gosub 350:rem xscroll
	gosub 450:rem bitmap flag
	gosub 550:rem semigraphics flag
	gosub 650:rem line length
	gosub 750:rem line increment
	gosub 850:rem display enable
	gosub 950:rem display disable
	gosub 1050:rem char height
	gosub 1150:rem char width
	gosub 1250:rem underline
	for j = 0 to 15
		char 1, 40, j
		a = 16 * j
		gosub 2000
		print "{rvon}";
		gosub 2000
		print "{rvof}";
		poke 241, 128
		print a;
	next
	for j = 0 to 2
		char 1, 41 + 2 * j, 22 + j
		a = 128 + 32
		gosub 2000
		a = 128 + 64 + 32
		gosub 2000
	next
	poke 241, 128:rem back to black, no revs or underline or flash
	char 1, 0, 20
	print " use cursor keys to scroll,
	print " letters to decrease register values,
	print " shifted letters to increase.
	sys wx, de, 34
	sys wx, dd, 35
	sys wx, ll, 1:sys wx, li, 27
	sys dec("cd6f"):rem enable cursor
	do
		print "{home}";:rem put hw cursor where we can see it
		getkey a$
		a = instr("{up}{down}r{left}{rght}bslLiIeEdDhHwWuU", a$)
		on a gosub 200,210,260,300,310,400,500,600,610,700,710,800,810,900,910,1000,1010,1100,1110,1200,1210
	loop

200	ys = (ys + 2) and 31:rem up
210	ys = (ys - 1) and 31:rem down
	gosub 275
250	char 1, 26, 6:print ys "{left}  "
	return

260	fr = 64 - fr:rem toggle reverse flag
	gosub 275
270	char 1, 26, 16:print fr "{left}  "
	return

275	sys wx, fr + 32 + ys, 24:rem slowflash
	return

300	xs = (xs - 2) and 15:rem left
310	xs = (xs + 1) and 15:rem right
	gosub 575
350	char 1, 26, 5:print xs "{left}  "
	return

400	fb = 128 - fb:rem toggle bitmap flag
	gosub 575
450	char 1, 26, 15:print fb "{left}  "
	return

500	fs = 32 - fs:rem toggle semi graphics
	gosub 575
550	char 1, 26, 17:print fs "{left}  "
	return

575	sys wx, fb + 64 + fs + xs, 25:rem bitmap, attr, semi
	return

600	ll = (ll - 2) and 255:rem line length
610	ll = (ll + 1) and 255
	sys wx, ll, 1
650	char 1, 26, 9:print ll "{left}  "
	return

700	li = (li - 2) and 255:rem line increment
710	li = (li + 1) and 255
	sys wx, li, 27
750	char 1, 26, 10:print li "{left}  "
	return

800	de = (de - 2) and 255:rem display enable
810	de = (de + 1) and 255
	sys wx, de, 34
850	char 1, 26, 12:print de "{left}  "
	return

900	dd = (dd - 2) and 255:rem display disable
910	dd = (dd + 1) and 255
	sys wx, dd, 35
950	char 1, 26, 13:print dd "{left}  "
	return

1000	ch = (ch - 2) and 31:rem character height
1010	ch = (ch + 1) and 31
	sys wx, ch, 23
1050	char 1, 26, 8:print ch "{left}  "
	return

1100	cw = (cw - 2) and 15:rem character width
1110	cw = (cw + 1) and 15
	sys rx,, 22:rreg a:a = a and 240
	sys wx, a + cw, 22
1150	char 1, 26, 7:print cw "{left}  "
	return

1200	ul = (ul - 2) and 31:rem underline
1210	ul = (ul + 1) and 31
	sys wx, ul, 29
1250	char 1, 26, 11:print ul "{left}  "
	return

2000	for i = 0 to 15
		poke 241, a + i:rem change color
		print chr$(64 + i);
	next
	return

;ACME 0.95.7
; driver for Atari ST mouse

; CIA #1, $dc01: %76543210
;	bit7	bit6	bit5	bit4	bit3	bit2	bi1	bit0
;	keyb,	keyb,	keyb,	LBUT,	YA,	YB,	XA,	XB	(swapped YA/YB names to make y axis point down)

; signal change sequences on pins 4321 (bits 3210):
; '_' means don't care, "->" shows flow of time, all sequences wrap around
; up	| 00__ -> 01__ -> 11__ -> 10__
; down	| 00__ -> 10__ -> 11__ -> 01__
; left	| __00 -> __01 -> __11 -> __10
; right	| __00 -> __10 -> __11 -> __01

	!zone atarist

.transmogrified	!byte 0

atarist_idle ; called as often as possible
		lda state_now
		lsr
		eor state_now
		sta .transmogrified
; Atari: ..., YA^YB, don't care, XA^XB
	.BIT_XA	= %0010
	.BIT_YA	= %1000	; this does not match official Atari documentation, because Atari's Y axis points up instead of down!
	.TRANSMOGRIFIED_X	= %0001
	.TRANSMOGRIFIED_Y	= %0100
		; check for X change
		lda state_change
		and #.BIT_XA
		beq +
			; X coordinate has changed
			ldx #ENUM_ATARIST
			lda #.TRANSMOGRIFIED_X
			bit .transmogrified
			jsr change1
+		; check for Y change
		lda state_change
		and #.BIT_YA
		beq +
			; Y coordinate has changed
			ldx #ENUM_ATARIST + ENUM_TOTAL	; means "y stuff"
			lda #.TRANSMOGRIFIED_Y
			bit .transmogrified
			jsr change1
+		rts
